apply plugin: 'com.android.application'

android {
    compileSdkVersion 19 // 'android-L'
    buildToolsVersion '20.0.0'

    defaultConfig {
        applicationId "com.podcatcher.deluxe"
        minSdkVersion 14
        targetSdkVersion 19 // "L"
    }
    signingConfigs {
        audioSimple {
            storeFile file("Podcatcher KeyStore")
            storePassword System.getenv("PCDKSTOREPWD")
            keyAlias "simple podcatcher"
            keyPassword System.getenv("PCDKEYPWD")
        }
        audioDeluxe {
            storeFile file("Podcatcher KeyStore")
            storePassword System.getenv("PCDKSTOREPWD")
            keyAlias "deluxe podcatcher"
            keyPassword System.getenv("PCDKEYPWD")
        }
        videoDeluxe {
            storeFile file("Podcatcher KeyStore")
            storePassword System.getenv("PCDKSTOREPWD")
            keyAlias "video podcatcher"
            keyPassword System.getenv("PCDKEYPWD")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        play {
            runProguard true
            proguardFiles 'proguard-rules.pro'
        }
        amazon {
            runProguard true
            proguardFiles 'proguard-rules.pro'
        }
        slideme {
            runProguard true
            proguardFiles 'proguard-rules.pro'
        }
        samsung {
            runProguard true
            proguardFiles 'proguard-rules.pro'
        }
    }

    flavorDimensions "media", "edition"
    productFlavors {
        def audioSimpleSigningVariable = signingConfigs.audioSimple
        def audioDeluxeSigningVariable = signingConfigs.audioDeluxe
        def videoDeluxeSigningVariable = signingConfigs.videoDeluxe

        simple {
            flavorDimension "edition"
            applicationId "net.alliknow.podcatcher"
            versionCode 200
            versionName "3.0.6"

            signingConfig audioSimpleSigningVariable
        }
        deluxe {
            flavorDimension "edition"
        }
        audio {
            flavorDimension "media"
            applicationId "com.podcatcher.deluxe"
            versionCode 201
            versionName "4.0.6"

            signingConfig audioDeluxeSigningVariable
        }
        video {
            flavorDimension "media"
            applicationId "com.podcatcher.deluxe.video"
            versionCode 202
            versionName "5.0.6"

            signingConfig videoDeluxeSigningVariable
        }
    }
}

dependencies {
    // These are the libs to include
    compile project(':gpodder_sync')
    compile project(':color_picker')
    compile files('libs/dropbox-sync-sdk-android.jar')
    compile files("$buildDir/native-libs/native-libs.jar")

    compile fileTree(include: ['*.jar'], dir: 'libs')
    // You must install or update the Support Repository through the SDK manager to use this dependency.
    compile 'com.android.support:support-v4:19.+'
    compile 'com.android.support:support-annotations:+'
}

task nativeLibsToJar(type: Zip) {
    destinationDir file("$buildDir/native-libs")
    baseName 'native-libs'
    extension 'jar'
    from fileTree(dir: 'libs', include: '**/*.so')
    into 'lib/'
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn(nativeLibsToJar)
}